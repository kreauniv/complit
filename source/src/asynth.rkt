#lang racket

(provide write-wav-file 
         (struct-out step)
         konst clock phasor oscil noise midi2hz mix mod
         line expon decay stitch cut after
         d adsr fadein fadeout crossfade
         lpf bpf bpf0 hpf)

(require racket/match)

; A struct capturing the result of a sample generation step.
(struct step (val gen))
                           
(define *default-sampling-rate* 48000)

(define (write-wav-file filename
                        model
                        duration
                        [sample-rate *default-sampling-rate*])
  (define dt (/ 1.0 sample-rate))
  (define num-samples (exact-round (* duration sample-rate)))
  (define num-channels 1)
  (define bytes-per-sample 4)
  (define bits-per-sample (* 8 bytes-per-sample))
  (define byte-rate (* sample-rate num-channels bytes-per-sample))
  (define block-align (* num-channels bytes-per-sample))
  (define data-size (* num-samples num-channels bytes-per-sample))
  (define file-size (+ 36 data-size))

  (call-with-output-file filename #:exists 'replace
    (lambda (out)
      (write-RIFF-header file-size out)
      (write-fmt-subchunk num-channels sample-rate byte-rate block-align bits-per-sample out)
      (write-data-chunk data-size out)
      (let loop ([i 0]
                 [bytes (make-bytes 4)]
                 [s model])
        (let ([v (s dt)])
          (when (< i num-samples)
            (real->floating-point-bytes (step-val v)
                                        4 ; bytes per value
                                        #f ; little-endian
                                        bytes
                                        )
            (write-bytes bytes out)
            (loop (+ i 1) bytes (step-gen v))))))))

(define (write-RIFF-header file-size out)
  (write-bytes #"RIFF" out)
  (write-int32le file-size out)
  (write-bytes #"WAVE" out))

(define (write-fmt-subchunk num-channels sample-rate byte-rate block-align bits-per-sample out)
  (write-bytes #"fmt " out)
  (write-int32le 16 out)    ; subchunk size
  (write-int16le 3 out)     ; audio format (3 = IEEE float)
  (write-int16le num-channels out)
  (write-int32le sample-rate out)
  (write-int32le byte-rate out)
  (write-int16le block-align out)
  (write-int16le bits-per-sample out))

(define (write-data-chunk data-size out)
  (write-bytes #"data" out)
  (write-int32le data-size out))

(define (write-int16le n out)
  (write-byte (bitwise-and n #xFF) out)
  (write-byte (bitwise-and (arithmetic-shift n -8) #xFF) out))

(define (write-int32le n out)
  (write-byte (bitwise-and n #xFF) out)
  (write-byte (bitwise-and (arithmetic-shift n -8) #xFF) out)
  (write-byte (bitwise-and (arithmetic-shift n -16) #xFF) out)
  (write-byte (bitwise-and (arithmetic-shift n -24) #xFF) out))


; The "konst" primitive (for "constant") simply always produces the same
; sample value every time the model is asked for one.
(define (konst v)
  (lambda (dt)
    (step v (konst v))))

; The "clock" simply accumulates the sample intervals to compute the "time".
; This is not technically an "audio" model as the clock is not intended to be
; heard, but it is nevertheless how time keeping needs to be done. We'll permit
; the clock to change its speed over time, and so make the "speed" of the clock
; also a model.

(define (clock speed (t 0.0))
  (lambda (dt)
    (let ([s (speed dt)])
      ; Step the time to the sig value by getting the current speed
      ; and scaling the given dt by the speed. So if speed produces
      ; 2.0, the clock will advance twice as fast.
      (step t (clock (step-gen s) (+ t (* (step-val s) dt)))))))

; Given a Gen that produces a stream of values and a function that
; maps a value to another, we can define a general operation of 
; mapping the function over the Gen.

(define (mapgen fn s)
  (lambda (dt)
    (let ([v (s dt)])
      (step (fn (step-val v))
            (mapgen fn (step-gen v))))))

(define (mapgen2 fn s1 s2)
  (lambda (dt)
    (let ([v1 (s1 dt)]
          [v2 (s2 dt)])
      (step (fn (step-val v1) (step-val v2))
            (mapgen2 fn (step-gen v1) (step-gen v2))))))

; A "phasor" is a simpler version of the clock which goes periodically from 0
; to 1 over a duration determined by a given "frequency".

(define (phasor f)
  (mapgen (lambda (x) (- x (floor x)))
          (clock f 0.0)))

; The noise is scaled to [-1,1] range.
(define noise
  (lambda (dt)
    (step (* 2.0 (- (random) 0.5)) noise)))

; An "oscil" is a sinusoidal oscillator. This can be easily derived from a
; phasor, so we make use of the phasor.

(define (oscil f)
  (mapgen (lambda (x) (sin (* 2 pi x)))
          (phasor f)))

; MIDI note numbers have a logarithmic relationship to frequency in Hz.
; middle octave A is MIDI note number 69 which is set at 440Hz for tuning purposes.
; And a octave is a frequency doubling (or halving) and an octave is 12 MIDI notes
; higher (or lower) from wherever you are.
(define (midi2hz notenum)
  (mapgen (lambda (n) (* 440.0 (exp (* (log 2) (/ (- n 69) 12)))))
          notenum))

; The simplest operation that combines two audio Gens is to "mix" them. This
; is simple addition of the sample values produced by the processes. However,
; you do need to be aware of whether the step can exceed the [-1,1] range.

(define (mix a b)
  (mapgen2 + a b))

; "Modulation" refers to controlling the amplitude of one form with another
; form. This is often just the multiplication of two Gens. So we'll use that
; simple definition.

(define (mod a b)
  (mapgen2 * a b))

; Controlling Gens often needs linear ramps as a primitive. So we'll make
; one. When given two values and a duration, a "line" will start at the
; starting value and change linearly to the ending value.

(define (line a dur b)
  (stitch (mapgen (λ (t) (+ a (* t (- b a))))
                  (clock (konst (/ 1.0 dur)) 0.0))
          dur
          (konst b)))

; expon is like line except it make an exponential curve instead of a linear one
; between the two end values a and b.
(define (expon a dur b)
  (let ([la (log a)]
        [lb (log b)])
    (stitch (mapgen (λ (t) (real-part (exp (+ la (* t (- lb la))))))
                    (clock (konst (/ 1.0 dur)) 0.0))
            dur
            (konst b))))

; An exponential "decay" starts at some value and goes to 0 exponentially at some
; rate.
(define (decay halflife v)
  (if (< (abs v) 1/32768)
      (konst 0.0)
      (lambda (dt)
        (step v (decay halflife (* v (exp (- (* (log 2) (/ dt halflife))))))))))

; It is useful to be able to sequence two models in time. In this case, we want to
; stop processing the preceding Gen once its stipulated duration completes, and then
; switch to processing the second Gen.
(define (stitch a dur b)
  (if (<= dur 0.0)
      ; If no duration left, then it's all b afterwards.
      ; Notice that stitch does not get used recursively
      ; in this branch, which means it's all really b from
      ; this point onwards.
      b
      (lambda (dt)
        ; Keep reducing the duration as we run a.
        (match-let ([(step av ag) (a dt)])
          (step av (stitch ag (- dur dt) b))))))

; Single time step delay.
(define (d g (v 0.0))
  (lambda (dt)
    (step v g)))

; Recursively defined n-step delay.
(define (dn n g)
  (if (= n 0)
      g
      (d (dn (- n 1) g))))          
        
; We can now use the core primitives we've defined above to express some common
; constructs that are useful.

; A cut can be expressed in terms of stitch.
(define (cut dur g)
  (stitch g dur (konst 0.0)))

; Likewise `after` can be expressed in terms of stitch too.
(define (after dur g)
  (stitch (konst 0.0) dur g))

; The "ADSR" curve refers to a four segment curve with an "attack" period over
; which the value rises from 0.0 to some peak, followed by a short "decay"
; period when the value reduces to a "sustain level", which then lasts for a
; "sustain duration" before getting "released" back to 0.0 over the release
; duration. We can therefore express ADSR as a sequence of line segments.

(define (adsr alevel adur decay slevel sdur release)
  (stitch (line 0.0 adur alevel)
          adur
          (stitch (line alevel decay slevel)
                  decay
                  (stitch (line slevel sdur slevel)
                          sdur
                          (line slevel release 0.0)))))

; A "fadein" modifies an audio clip by ramping up its volume from 0.0 to 1.0
; over a specified duration. Similarly, a "fadeout" ramps the volume from 1.0
; to 0.0 at the end. This enables smooth entry and exit of an audio clip.

(define (fadein xdur)
  (line 0.0 xdur 1.0))

(define (fadeout xdur totaldur)
  (stitch (konst 1.0)
          (- totaldur xdur)
          (line 1.0 xdur 0.0)))

; A "crossfade" refers to two audio snippets that are partially overlapped to
; "join" them smoothly. Over this overlap period, the preceding audio snippet
; "fades out" while the succeeding audio snippet "fades in". This is a more
; practical form of sequencing audio snippets. We can use line, stitch, mix and
; mod to express this combination approach.

(define (crossfade xdur a adur b)
  (mix (cut adur (mod (fadeout adur xdur) a))
       (after (- adur xdur)
              (mod (fadein xdur) b))))


; So now we have a bunch of audio operators and methods to combine them so we can
; construct Gens and render them to a file for playback.

; Below are some common "biquad filters" that are useful for synthesis.
; I've included them here just to make the point that we can continue
; modelling more operators using the same approach. Understanding the
; math here requires getting into digital signal processing and that's
; not a requirement. Just understand that `lpf` cuts of higher frequencies,
; `bpf` selects frequencies around a given value and `hpf` cuts off lower
; frequencies. `lpf` is useful to "smooth" sounds, "hpf" is useful to get
; rid of offsets and "bpf" is useful as model of resonance. This isn't
; an exhaustive list.

(define (biquad b0 b1 b2 a0 a1 a2 xn xn1 xn2 yn1 yn2)
  (/ (+ (* b0 xn) (* b1 xn1) (* b2 xn2)
        (* -1 a1 yn1) (* -1 a2 yn2))
     a0))

(define (lpf freq q sr g)
  (let* ([dt (/ 1.0 sr)]
         [w0 (* 2 pi freq dt)]
         [sw0 (sin w0)]
         [cw0 (cos w0)]
         [alpha (/ sw0 (* 2 q))]
         [b1 (- 1.0 cw0)]
         [b0 (/ b1 2.0)]
         [b2 (/ b1 2.0)]
         [a0 (+ 1.0 alpha)]
         [a1 (* -2.0 cw0)]
         [a2 (- 1.0 alpha)])
    (define (lpf* x xn1 xn2 yn1 yn2)
      (lambda (dt)
        (match-let ([(step xn xg) (x dt)])
          (let ([yn (biquad b0 b1 b2 a0 a1 a2 xn xn1 xn2 yn1 yn2)])
            (step yn (lpf* xg xn xn1 yn yn1))))))
    (lpf* g 0.0 0.0 0.0 0.0)))

(define (bpf freq q sr g)
  (let* ([dt (/ 1.0 sr)]
         [w0 (* 2 pi freq dt)]
         [sw0 (sin w0)]
         [cw0 (cos w0)]
         [alpha (/ sw0 (* 2 q))]
         [b0 sw0]
         [b1 0.0]
         [b2 (- b0)]
         [a0 (+ 1.0 alpha)]
         [a1 (* -2.0 cw0)]
         [a2 (- 1.0 alpha)])
    (define (bpf* x xn1 xn2 yn1 yn2)
      (lambda (dt)
        (match-let ([(step xn xg) (x dt)])
          (let ([yn (biquad b0 b1 b2 a0 a1 a2 xn xn1 xn2 yn1 yn2)])
            (step yn (bpf* xg xn xn1 yn yn1))))))
    (bpf* g 0.0 0.0 0.0 0.0)))

(define (bpf0 freq q sr g)
  (let* ([dt (/ 1.0 sr)]
         [w0 (* 2 pi freq dt)]
         [sw0 (sin w0)]
         [cw0 (cos w0)]
         [alpha (/ sw0 (* 2 q))]
         [b0 alpha]
         [b1 0.0]
         [b2 (- b0)]
         [a0 (+ 1.0 alpha)]
         [a1 (* -2.0 cw0)]
         [a2 (- 1.0 alpha)])
    (define (bpf0* x xn1 xn2 yn1 yn2)
      (lambda (dt)
        (match-let ([(step xn xg) (x dt)])
          (let ([yn (biquad b0 b1 b2 a0 a1 a2 xn xn1 xn2 yn1 yn2)])
            (step yn (bpf0* xg xn xn1 yn yn1))))))
    (bpf0* g 0.0 0.0 0.0 0.0)))

(define (hpf freq q sr g)
  (let* ([dt (/ 1.0 sr)]
         [w0 (* 2 pi freq dt)]
         [sw0 (sin w0)]
         [cw0 (cos w0)]
         [alpha (/ sw0 (* 2 q))]
         [b0 (/ (+ 1.0 cw0) 2.0)]
         [b1 (* -2.0 b0)]
         [b2 b0]
         [a0 (+ 1.0 alpha)]
         [a1 (* -2.0 cw0)]
         [a2 (- 1.0 alpha)])
    (define (hpf* x xn1 xn2 yn1 yn2)
      (lambda (dt)
        (match-let ([(step xn xg) (x dt)])
          (let ([yn (biquad b0 b1 b2 a0 a1 a2 xn xn1 xn2 yn1 yn2)])
            (step yn (hpf* xg xn xn1 yn yn1))))))
    (hpf* g 0.0 0.0 0.0 0.0)))
